/* SPDX-FileCopyrightText: 2026 LichtFeld Studio Authors
 * SPDX-License-Identifier: GPL-3.0-or-later */
#include "tile_rasterizer.hpp"
#include "frame_budget.hpp"
#include "gpu_profile.hpp"
#include "tile_shader_source.hpp"
#include <array>
#include <atomic>
#include <bit>
#include <cmath>
#include <format>
#include <map>
#include <mutex>
#include <stdexcept>
#include <string_view>
#include <vector>

namespace lfs::rendering::metal {
    namespace {
        // Keep in sync with the embedded Metal shader.
        constexpr uint32_t kDepthChunkSize = 576;
        // Below this length the extra summary/compose passes cost more than
        // serial blending in the real-scene corpus; keep that cheaper path.
        constexpr uint32_t kParallelMinTileInstances = 32768;
        uint32_t ceil_div(uint32_t n, uint32_t d) { return n / d + (n % d != 0); }
        struct alignas(16) RasterParameters {
            uint32_t count, width, height, columns, tiles, capacity, mode, unused;
            simd_float4 background;
            simd_float4 render_origin;
            simd_float4 intrinsics, clip;
            simd_uint4 camera;
            simd_float4 panorama;
            simd_uint4 mask_limits;
        };
        static_assert(sizeof(RasterParameters) == 144);
        struct SortParameters {
            uint32_t blocks, shift;
        };
        id<MTLBuffer> allocate(id<MTLDevice> device, size_t bytes,
                               MTLResourceOptions options = MTLResourceStorageModePrivate | MTLResourceHazardTrackingModeTracked) {
            bytes = std::max(bytes, sizeof(ProjectedSplat));
            if (bytes > device.maxBufferLength)
                throw std::length_error(std::format("Metal viewer buffer exceeds device limit (bytes={}, max={})", bytes, device.maxBufferLength));
            auto buffer = [device newBufferWithLength:bytes options:options];
            if (!buffer)
                throw std::runtime_error(std::format("Metal viewer GPU allocation failed (bytes={}, options={}, allocated={}, recommended={})", bytes, uint64_t(options), device.currentAllocatedSize, device.recommendedMaxWorkingSetSize));
            return buffer;
        }
        id<MTLTexture> texture(id<MTLDevice> device, uint32_t width, uint32_t height, MTLPixelFormat format) {
            auto descriptor = [MTLTextureDescriptor texture2DDescriptorWithPixelFormat:format
                                                                                 width:width
                                                                                height:height
                                                                             mipmapped:NO];
            descriptor.storageMode = MTLStorageModePrivate;
            descriptor.usage = MTLTextureUsageShaderRead | MTLTextureUsageShaderWrite;
            auto result = [device newTextureWithDescriptor:descriptor];
            if (!result)
                throw std::runtime_error(std::format("Metal viewer texture allocation failed (extent={}x{}, format={})", width, height, uint64_t(format)));
            return result;
        }
        struct ScanLevel {
            id<MTLBuffer> sums, offsets;
            uint32_t count;
        };
        using ScanStorage = std::vector<ScanLevel>;
        ScanStorage reserve_scan(id<MTLDevice> device, uint32_t n, size_t element_bytes = 8) {
            ScanStorage levels;
            for (;;) {
                const auto groups = ceil_div(n, 256);
                levels.push_back({allocate(device, size_t(groups) * element_bytes), allocate(device, size_t(groups) * element_bytes), n});
                if (groups <= 1)
                    break;
                n = groups;
            }
            return levels;
        }
    } // namespace

    struct RasterScratch::Impl {
        id<MTLDevice> device;
        id<MTLCommandQueue> queue;
        uint32_t max_splats = 0, capacity = 0, tiles = 0;
        id<MTLBuffer> counts, offsets, histogram, histogram_offsets, digit_offsets, ranges, dispatch_args;
        id<MTLBuffer> depth_jobs, partial_color, partial_depth, partial_pick;
        uint32_t parallel_instances = 0;
        std::array<id<MTLBuffer>, 2> keys, indices;
        ScanStorage count_scan;
    };
    RasterScratch::RasterScratch(id<MTLDevice> device) : impl_(std::make_shared<Impl>()) {
        if (!device)
            throw std::invalid_argument("Metal raster scratch requires a device");
        impl_->device = device;
    }
    RasterScratch::~RasterScratch() = default;
    bool RasterScratch::fits(uint32_t width, uint32_t height, uint32_t splats, uint32_t instances) const {
        return impl_->max_splats >= std::max(1u, splats) && impl_->capacity >= instances &&
               impl_->tiles >= uint64_t(ceil_div(width, 16)) * ceil_div(height, 16);
    }
    void RasterScratch::reserve(uint32_t tiles, uint32_t splats, uint32_t instances) {
        splats = std::max(1u, splats);
        if (impl_->max_splats >= splats && impl_->capacity >= instances && impl_->tiles >= tiles)
            return;
        // Existing commands retain their encoded buffer versions. Do not mutate
        // the published reservation until every required allocation succeeds.
        auto candidate = std::make_shared<Impl>(*impl_);
        auto& s = *candidate;
        const auto ensure = [&](id<MTLBuffer> __strong& buffer, size_t bytes) {
            if (!buffer || buffer.length < bytes)
                buffer = allocate(s.device, bytes);
        };
        if (splats > s.max_splats) {
            ensure(s.counts, size_t(splats) * 8);
            ensure(s.offsets, size_t(splats) * 8);
            s.count_scan = reserve_scan(s.device, splats);
            s.max_splats = splats;
        }
        if (instances > s.capacity) {
            for (int n = 0; n < 2; ++n) {
                ensure(s.keys[n], size_t(instances) * 8);
                ensure(s.indices[n], size_t(instances) * 4);
            }
            const auto histogram_size = size_t(ceil_div(instances, 2048)) * 256;
            ensure(s.histogram, histogram_size * 4);
            ensure(s.histogram_offsets, histogram_size * 4);
            ensure(s.digit_offsets, 257 * sizeof(uint32_t));
            ensure(s.dispatch_args, 21 * sizeof(uint32_t));
            s.capacity = instances;
        }
        if (tiles > s.tiles) {
            ensure(s.ranges, size_t(tiles) * 8);
            // Optional parallel jobs include one sentinel per tile. Re-admit
            // their full allocation for the larger layout on a later dense frame.
            s.parallel_instances = 0;
            s.depth_jobs = s.partial_color = s.partial_depth = s.partial_pick = nil;
            s.tiles = tiles;
        }
        impl_ = std::move(candidate);
    }
    struct RasterFrame::Impl {
        id<MTLDevice> device;
        uint32_t width, height, max_splats, capacity, tiles, columns, sort_blocks;
        id<MTLBuffer> status;
        id<MTLTexture> color, depth, pick;
        std::shared_ptr<RasterScratch> scratch;
        bool shared_scratch = false;
        std::atomic_bool in_flight{false};
        std::atomic_bool completed{false};
        uint32_t previous_source_count = 0;
    };
    RasterFrame::RasterFrame(id<MTLDevice> device, uint32_t width, uint32_t height,
                             uint32_t max_splats, uint32_t max_instances, std::shared_ptr<RasterScratch> scratch) : impl_(std::make_shared<Impl>()) {
        if (!device || !width || !height || width > 16384 || height > 16384 || !max_instances ||
            (scratch && scratch->impl_->device != device))
            throw std::invalid_argument(std::format("Invalid Metal viewer frame reservation (device_present={}, extent={}x{}, max_splats={}, max_instances={})", device != nil, width, height, max_splats, max_instances));
        auto& f = *impl_;
        f.device = device;
        f.width = width;
        f.height = height;
        f.max_splats = max_splats;
        f.capacity = max_instances;
        f.columns = ceil_div(width, 16);
        f.tiles = f.columns * ceil_div(height, 16);
        f.sort_blocks = ceil_div(max_instances, 2048);
        f.shared_scratch = bool(scratch);
        f.scratch = scratch ? std::move(scratch) : std::make_shared<RasterScratch>(device);
        f.scratch->reserve(f.tiles, max_splats, max_instances);
        f.status = allocate(device, sizeof(RasterStatus), MTLResourceStorageModeShared);
        f.color = texture(device, width, height, MTLPixelFormatRGBA16Float);
        f.depth = texture(device, width, height, MTLPixelFormatRGBA32Float);
        f.pick = texture(device, width, height, MTLPixelFormatR32Uint);
    }
    RasterFrame::~RasterFrame() = default;
    bool RasterFrame::busy() const { return impl_->in_flight.load(std::memory_order_acquire); }
    RasterStatus RasterFrame::status() const {
        if (busy() || !impl_->completed.load(std::memory_order_acquire))
            throw std::logic_error(std::format("Metal viewer status requires successful GPU completion (busy={}, completed={})", busy(), impl_->completed.load(std::memory_order_acquire)));
        return *static_cast<const RasterStatus*>(impl_->status.contents);
    }
    id<MTLTexture> RasterFrame::color() const { return impl_->color; }
    id<MTLTexture> RasterFrame::depth() const { return impl_->depth; }
    id<MTLTexture> RasterFrame::pick() const { return impl_->pick; }
    id<MTLBuffer> RasterFrame::statusBuffer() const { return impl_->status; }

    struct TileRasterizer::Impl {
        id<MTLDevice> device;
        std::map<std::string, id<MTLComputePipelineState>> pipelines;
        id<MTLLibrary> library, relaxed_library;
        std::mutex blend_mutex;
        std::map<std::pair<uint32_t, uint32_t>, id<MTLComputePipelineState>> blend_pipelines;
        id<MTLComputePipelineState> blendPipeline(uint32_t mode, uint32_t flags) {
            std::lock_guard lock(blend_mutex);
            const auto key = std::pair{mode, flags};
            if (const auto found = blend_pipelines.find(key); found != blend_pipelines.end())
                return found->second;
            auto constants = [MTLFunctionConstantValues new];
            [constants setConstantValue:&mode type:MTLDataTypeUInt atIndex:0];
            [constants setConstantValue:&flags type:MTLDataTypeUInt atIndex:1];
            NSError* error = nil;
            auto blend_library = library;
            // Only opaque display blending permits reassociation. Coverage,
            // overlays, Portal/RAD and requested depth retain safe arithmetic.
            if ((flags & 4096u) && (mode == uint32_t(RasterMode::Gaussian) || mode == uint32_t(RasterMode::Gut)) &&
                !(flags & (1u | 2u | 4u | 8u | 16u | 64u | 2048u))) {
                if (@available(macOS 15.0, iOS 18.0, *)) {
                    if (!relaxed_library) {
                        auto options = [MTLCompileOptions new];
                        options.languageVersion = MTLLanguageVersion2_4;
                        options.mathMode = MTLMathModeRelaxed; // Honors Inf/NaN guards.
                        relaxed_library = [device newLibraryWithSource:[NSString stringWithUTF8String:kTileRasterizerSource]
                                                               options:options
                                                                 error:&error];
                        if (!relaxed_library)
                            throw std::runtime_error(std::format("Metal opaque blend compilation failed (error_code={}, error_domain={}, error={})", long(error.code), error.domain.UTF8String ?: "none", error.localizedDescription.UTF8String ?: "none"));
                    }
                    blend_library = relaxed_library;
                }
            }
            auto function = [blend_library newFunctionWithName:@"tile_blend" constantValues:constants error:&error];
            if (!function)
                throw std::runtime_error(std::format("Metal blend specialization failed (error_code={}, error_domain={}, error={})", long(error.code), error.domain.UTF8String ?: "none", error.localizedDescription.UTF8String ?: "none"));
            auto descriptor = [MTLComputePipelineDescriptor new];
            descriptor.computeFunction = function;
            descriptor.maxTotalThreadsPerThreadgroup = 64;
            descriptor.threadGroupSizeIsMultipleOfThreadExecutionWidth = YES;
            auto state = [device newComputePipelineStateWithDescriptor:descriptor options:MTLPipelineOptionNone reflection:nil error:&error];
            if (!state)
                throw std::runtime_error(std::format("Metal blend pipeline failed (error_code={}, error_domain={}, error={})", long(error.code), error.domain.UTF8String ?: "none", error.localizedDescription.UTF8String ?: "none"));
            if (state.threadExecutionWidth != 32 || state.maxTotalThreadsPerThreadgroup < 64)
                throw std::runtime_error(std::format("Metal blend specialization requires SIMD32 and 64-thread groups (width={}, max_threads={}, mode={}, flags={})", state.threadExecutionWidth, state.maxTotalThreadsPerThreadgroup, mode, flags));
            blend_pipelines.emplace(key, state);
            return state;
        }
        id<MTLComputeCommandEncoder> begin(id<MTLCommandBuffer> command, const char* name, GpuProfile* profile = nullptr, GpuStage stage = GpuStage::Sort) {
            auto encoder = profiledCompute(command, profile, stage);
            if (!encoder)
                throw std::runtime_error(std::format("Could not encode Metal viewer compute pass (name={}, status={})", name, long(command.status)));
            encoder.label = [NSString stringWithUTF8String:name];
            [encoder setComputePipelineState:pipelines.at(name)];
            return encoder;
        }
        void scan(id<MTLCommandBuffer> command, id<MTLBuffer> input, id<MTLBuffer> output,
                  uint32_t count, const ScanStorage& storage, size_t level = 0,
                  GpuProfile* profile = nullptr) {
            if (!count)
                return;
            const auto& scratch = storage.at(level);
            const auto groups = ceil_div(count, 256);
            auto encoder = begin(command, "scan_blocks", profile, GpuStage::Instances);
            [encoder setBuffer:input offset:0 atIndex:0];
            [encoder setBuffer:output offset:0 atIndex:1];
            [encoder setBuffer:scratch.sums offset:0 atIndex:2];
            [encoder setBytes:&count length:4 atIndex:3];
            const uint32_t primitive_counts = level == 0;
            [encoder setBytes:&primitive_counts length:4 atIndex:4];
            [encoder dispatchThreadgroups:MTLSizeMake(groups, 1, 1) threadsPerThreadgroup:MTLSizeMake(256, 1, 1)];
            [encoder endEncoding];
            if (groups <= 1)
                return;
            scan(command, scratch.sums, scratch.offsets, groups, storage, level + 1, profile);
            encoder = begin(command, "scan_add", profile, GpuStage::Instances);
            [encoder setBuffer:output offset:0 atIndex:0];
            [encoder setBuffer:scratch.offsets offset:0 atIndex:1];
            [encoder setBytes:&count length:4 atIndex:2];
            [encoder dispatchThreadgroups:MTLSizeMake(groups, 1, 1) threadsPerThreadgroup:MTLSizeMake(256, 1, 1)];
            [encoder endEncoding];
        }
    };
    TileRasterizer::TileRasterizer(id<MTLDevice> device) : impl_(std::make_unique<Impl>()) {
        if (!device)
            throw std::invalid_argument(std::format("Metal viewer requires a device (device_present={})", device != nil));
        impl_->device = device;
        auto options = [MTLCompileOptions new];
        options.languageVersion = MTLLanguageVersion2_4;
        if (@available(macOS 15.0, iOS 18.0, *)) {
            options.mathMode = MTLMathModeSafe;
            // Safe prevents reassociation but leaves exp/pow in Fast mode by
            // default. Median thresholds and Spark tails need precise FP32
            // intrinsics across Apple GPU generations, as fastMathEnabled=NO did.
            options.mathFloatingPointFunctions = MTLMathFloatingPointFunctionsPrecise;
        } else {
#pragma clang diagnostic push
#pragma clang diagnostic ignored "-Wdeprecated-declarations"
            options.fastMathEnabled = NO;
#pragma clang diagnostic pop
        }
        NSError* error = nil;
        auto library = [device newLibraryWithSource:[NSString stringWithUTF8String:kTileRasterizerSource]
                                            options:options
                                              error:&error];
        if (!library)
            throw std::runtime_error(std::format("Metal tile shader compilation failed (error_code={}, error_domain={}, error={})", long(error.code), error.domain.UTF8String ?: "none", error.localizedDescription.UTF8String ?: "none"));
        impl_->library = library;
        for (const char* name : {"tile_counts", "scan_blocks", "scan_add", "tile_status", "source_keys", "tile_instances",
                                 "tile_histogram", "tile_scatter", "source_histogram", "source_scatter", "source_ranges", "tile_ranges", "tile_depth_batches", "tile_depth_compose", "source_compact", "source_permutation", "digit_scan", "digit_offsets"}) {
            const bool source_keys = (std::string_view(name) == "source_histogram" || std::string_view(name) == "source_scatter" || std::string_view(name) == "source_ranges");
            const char* function_name = std::string_view(name) == "source_ranges" ? "tile_ranges"
                                        : source_keys                             ? (std::string_view(name) == "source_histogram" ? "tile_histogram" : "tile_scatter")
                                                                                  : name;
            auto constants = [MTLFunctionConstantValues new];
            [constants setConstantValue:&source_keys type:MTLDataTypeBool atIndex:2];
            auto function = [library newFunctionWithName:[NSString stringWithUTF8String:function_name]
                                          constantValues:constants
                                                   error:&error];
            auto descriptor = [MTLComputePipelineDescriptor new];
            descriptor.computeFunction = function;
            descriptor.maxTotalThreadsPerThreadgroup = 256;
            auto state = [device newComputePipelineStateWithDescriptor:descriptor options:MTLPipelineOptionNone reflection:nil error:&error];
            if (!state)
                throw std::runtime_error(std::format("Metal tile pipeline failed (error_code={}, error_domain={}, error={})", long(error.code), error.domain.UTF8String ?: "none", error.localizedDescription.UTF8String ?: "none"));
            if (state.threadExecutionWidth != 32 || state.maxTotalThreadsPerThreadgroup < 256)
                throw std::runtime_error(std::format("Metal viewer requires SIMD32 and 256-thread groups (function={}, simd_width={}, max_threads={})", function_name, state.threadExecutionWidth, state.maxTotalThreadsPerThreadgroup));
            impl_->pipelines.emplace(name, state);
        }
    }
    TileRasterizer::~TileRasterizer() = default;
    void TileRasterizer::encode(id<MTLCommandBuffer> command, BufferSlice projected, uint32_t count,
                                RasterMode mode, simd_float4 background, RasterFrame& frame, const OverlayBuffers& overlay, BufferSlice gut, const Projection& projection, const LodSelection& lod, bool omit_saturating_color, bool macro_half_display, GpuProfile* profile, bool exact_median) {
        auto f = frame.impl_;
        auto scratch = f->scratch->impl_;

        // Depth visualization must not select its median using rounded
        // display alpha, including transparent and portal presentation.
        // Transparent straight RGB is sensitive to both rounded coverage and
        // half-rounded footprints at the alpha cutoff. Use the analytic FP32
        // path; keep opaque marker/display matching on its existing profile.
        const bool precise_transparent = !macro_half_display && mode == RasterMode::Gaussian && background.w == 0.f && projection.display.z != 1.f;
        macro_half_display = macro_half_display && !exact_median && !precise_transparent;
        if (macro_half_display && (mode != RasterMode::Gaussian || projection.display.z == 1.f))
            throw std::invalid_argument(std::format("Macro half display requires ordinary 3DGS (mode={}, spark={})", uint32_t(mode), projection.display.z));
        if (!command || command.device != impl_->device || f->device != impl_->device ||
            command.status != MTLCommandBufferStatusNotEnqueued || count > f->max_splats || uint32_t(mode) > 3)
            throw std::invalid_argument(std::format("Invalid Metal viewer frame submission (command_present={}, status={}, count={}, max_splats={}, mode={}, same_device={})", command != nil, long(command.status), count, f->max_splats, uint32_t(mode), command.device == impl_->device));
        if (f->shared_scratch && scratch->queue && scratch->queue != command.commandQueue)
            throw std::invalid_argument("Shared Metal raster scratch requires one serialized command queue");
        if (f->shared_scratch && command)
            scratch->queue = command.commandQueue;
        for (int i = 0; i < 4; ++i)
            if (!std::isfinite(background[i]) || background[i] < 0 || (i == 3 && background[i] > 1))
                throw std::invalid_argument(std::format("Invalid Metal viewer background (component={}, value={})", i, float(background[i])));
        if (count && (!projected.buffer || projected.buffer.device != impl_->device || projected.offset % 16 ||
                      projected.offset > projected.buffer.length ||
                      size_t(count) * sizeof(ProjectedSplat) > projected.buffer.length - projected.offset))
            throw std::invalid_argument(std::format("Invalid Metal viewer projected splat buffer (count={}, offset={}, length={}, required_bytes={}, same_device={})", count, projected.offset, projected.buffer.length, size_t(count) * sizeof(ProjectedSplat), projected.buffer.device == impl_->device));
        if (mode == RasterMode::Gut && count &&
            (!gut.buffer || gut.buffer.device != impl_->device || gut.offset % 16 ||
             gut.offset > gut.buffer.length || size_t(count) * sizeof(GutSplat) > gut.buffer.length - gut.offset))
            throw std::invalid_argument(std::format("Invalid native 3DGUT geometry buffer (count={}, offset={}, length={}, required_bytes={})", count, gut.offset, gut.buffer.length, size_t(count) * sizeof(GutSplat)));
        if (mode == RasterMode::Gut) {
            for (int component = 0; component < 4; ++component)
                if (!std::isfinite(projection.intrinsics[component]) || !std::isfinite(projection.clip_scale[component]))
                    throw std::invalid_argument(std::format("Invalid native 3DGUT ray parameters (component={}, intrinsics={}, clip={})", component, float(projection.intrinsics[component]), float(projection.clip_scale[component])));
            if (projection.intrinsics.x <= 0 || projection.intrinsics.y <= 0 || projection.clip_scale.x <= 0 || projection.extent.z > uint32_t(CameraModel::Equirectangular))
                throw std::invalid_argument(std::format("Invalid native 3DGUT camera (focal=({}, {}), near={}, camera={})", projection.intrinsics.x, projection.intrinsics.y, projection.clip_scale.x, projection.extent.z));
            if (projection.extent.z == uint32_t(CameraModel::Equirectangular)) {
                for (int component = 0; component < 4; ++component)
                    if (!std::isfinite(projection.panorama[component]))
                        throw std::invalid_argument(std::format("Invalid native 3DGUT panorama (component={}, value={})", component, float(projection.panorama[component])));
                const auto panorama = projection.panorama;
                if (panorama.x < f->width || panorama.y < f->height || panorama.x > 65535 || panorama.y > 65535 ||
                    panorama.z < 0 || panorama.w < 0 || panorama.z + f->width > panorama.x || panorama.w + f->height > panorama.y)
                    throw std::invalid_argument(std::format("Invalid native 3DGUT panorama subregion (camera={}x{}, origin=({}, {}), extent={}x{})", panorama.x, panorama.y, panorama.z, panorama.w, f->width, f->height));
            }
        }
        const bool expected_depth = projection.rasterization.y == 1.f;
        if (!std::isfinite(projection.rasterization.y) || (projection.rasterization.y != 0.f && !expected_depth) ||
            (expected_depth && (!std::isfinite(projection.rasterization.z) || projection.rasterization.z <= 0)))
            throw std::invalid_argument(std::format("Invalid Metal expected-depth capture parameters (enabled={}, max_depth={})", projection.rasterization.y, projection.rasterization.z));
        const auto logical = lod.logical_indices.buffer ? lod.logical_indices : lod.indices;
        if (lod.enabled && (lod.count != count || !lod.source_count ||
                            (count && (!logical.buffer || logical.buffer.device != impl_->device || logical.offset % 4 ||
                                       logical.offset > logical.buffer.length || size_t(count) * 4 > logical.buffer.length - logical.offset))))
            throw std::invalid_argument(std::format("Invalid native Metal LOD identifier mapping (lod_count={}, count={}, source_count={}, offset={}, length={})", lod.count, count, lod.source_count, logical.offset, logical.buffer.length));
        const uint32_t logical_count = lod.enabled ? (lod.logical_count ? lod.logical_count : lod.source_count) : count;
        const uint32_t selection_count = overlay.selection.buffer ? (overlay.selection_count ? overlay.selection_count : logical_count) : 0;
        const uint32_t preview_count = overlay.preview.buffer ? (overlay.preview_count ? overlay.preview_count : logical_count) : 0;
        const auto check_mask = [&](BufferSlice mask, uint32_t extent) {
            if (extent && (mask.buffer.device != impl_->device || mask.offset > mask.buffer.length || extent > mask.buffer.length - mask.offset))
                throw std::invalid_argument(std::format("Metal logical selection mask exceeds its resident storage (extent={}, offset={}, length={}, same_device={})", extent, mask.offset, mask.buffer.length, mask.buffer.device == impl_->device));
        };
        check_mask(overlay.selection, selection_count);
        check_mask(overlay.preview, preview_count);
        bool single_simd = mode == RasterMode::Gaussian && projection.display.z != 1.f;
        RasterParameters p{count, f->width, f->height, f->columns, f->tiles, f->capacity, uint32_t(mode), (overlay.parameter_count ? 1u : 0u) | (expected_depth ? 2u : 0u) | (projection.rasterization.w == 1.f && projection.display.z == 0 ? 4u : 0u) | (lod.enabled ? 8u : 0u) | (projection.display.z == 1.f ? 16u : 0u) | (omit_saturating_color ? 32u : 0u) | (macro_half_display ? 64u : 0u) | (single_simd ? 128u : 0u) | (exact_median ? 2048u : 0u) | (background.w == 1.f ? 4096u : 0u) | (precise_transparent ? 16384u : 0u), background, overlay.render_origin, projection.intrinsics, {projection.clip_scale.x, expected_depth ? projection.rasterization.z : projection.clip_scale.y, projection.clip_scale.z, projection.clip_scale.w}, projection.extent, projection.panorama, {selection_count, preview_count, 0, 0}};
        // Keep periodic GS arithmetic out of the perspective/orthographic blend
        // specialization. A runtime branch in the contributor loop prevents the
        // compiler from retaining its compact ordinary-GS arithmetic.
        if (projection.extent.z == uint32_t(CameraModel::Equirectangular) && mode != RasterMode::Gut)
            p.unused |= 8192u;
        // Compile/cache the default before reserving the frame or encoding work. A
        // specialization failure cannot strand its busy flag or partial scratch.
        auto blend_pipeline = impl_->blendPipeline(uint32_t(mode), p.unused);
        id<MTLComputePipelineState> prefix_pipeline = nil;
        bool depth_batches = false;
        if (f->in_flight.exchange(true, std::memory_order_acq_rel))
            throw std::logic_error(std::format("Metal viewer frame reservation is still in flight (extent={}x{}, capacity={}, count={})", f->width, f->height, f->capacity, count));
        // Completion already publishes this shared status; inspecting the last
        // finished frame adds no GPU readback, allocation or synchronization.
        // Extremely sparse frames keep the original intersection sort. A changing
        // camera can choose the less efficient path for one frame, but both
        // paths preserve the exact full-width key and original source IDs.
        bool source_sorted = false;
        try {
            if (count >= 4096 && count <= f->capacity && f->previous_source_count == count &&
                f->completed.load(std::memory_order_acquire)) {
                const auto previous = *static_cast<const RasterStatus*>(f->status.contents);
                // The former sort-buffer alias only admitted this optimization
                // at small extents. Own scratch follows completed dense counts,
                // leaving the maximum sort reservation and sparse frames alone.
                if (mode == RasterMode::Gaussian && !(p.unused & (1u | 4u | 8u | 16u | 32u | 64u | 8192u)) &&
                    previous.error == RasterError::None &&
                    previous.maximum_tile_instances > kParallelMinTileInstances) {
                    const uint32_t needed = uint32_t(std::min<uint64_t>(f->capacity, previous.required_instances + previous.required_instances / 8));
                    if (needed > scratch->parallel_instances) {
                        const size_t slots = ceil_div(needed, kDepthChunkSize) + size_t(scratch->tiles);
                        const size_t rgba_bytes = slots * 256 * 16, pick_bytes = slots * 256 * 4, job_bytes = slots * 8;
                        const uint64_t allocated = impl_->device.currentAllocatedSize, recommended = impl_->device.recommendedMaxWorkingSetSize;
                        if (rgba_bytes <= impl_->device.maxBufferLength && pick_bytes <= impl_->device.maxBufferLength &&
                            frameFitsWorkingSet(allocated, rgba_bytes * 2 + pick_bytes + job_bytes, recommended)) {
                            auto jobs = [impl_->device newBufferWithLength:job_bytes options:MTLResourceStorageModePrivate];
                            auto colors = [impl_->device newBufferWithLength:rgba_bytes options:MTLResourceStorageModePrivate];
                            auto depths = [impl_->device newBufferWithLength:rgba_bytes options:MTLResourceStorageModePrivate];
                            auto picks = [impl_->device newBufferWithLength:pick_bytes options:MTLResourceStorageModePrivate];
                            if (jobs && colors && depths && picks) {
                                scratch->depth_jobs = jobs;
                                scratch->partial_color = colors;
                                scratch->partial_depth = depths;
                                scratch->partial_pick = picks;
                                scratch->parallel_instances = needed;
                            }
                        }
                    }
                    if (scratch->parallel_instances) {
                        depth_batches = true;
                        p.unused |= 512u;
                        p.mask_limits.z = scratch->parallel_instances;
                        blend_pipeline = impl_->blendPipeline(uint32_t(mode), p.unused);
                        prefix_pipeline = impl_->blendPipeline(uint32_t(mode), p.unused | 1024u);
                    }
                }
                source_sorted = previous.error == RasterError::None &&
                                previous.required_instances > uint64_t(count) / 4;
                if (mode == RasterMode::Gut && previous.error == RasterError::None &&
                    previous.required_instances > uint64_t(f->tiles) * 512) {
                    // Cache SIMD32 only when a completed dense frame needs it.
                    // Sparse scenes retain their original pipeline and avoid
                    // compiling a large unused shader variant during startup.
                    blend_pipeline = impl_->blendPipeline(uint32_t(mode), p.unused | 128u);
                    single_simd = true;
                    p.unused |= 128u;
                }
            }
        } catch (...) {
            // No raster work has been encoded and completed/status metadata is
            // untouched. A specialization failure must release the reservation.
            f->in_flight.store(false, std::memory_order_release);
            throw;
        }
        f->previous_source_count = count;
        if (source_sorted)
            p.unused |= 256u;
        f->completed.store(false, std::memory_order_release);
        // A committed command retains the frame and all scratch until its completion.
        [command addCompletedHandler:^(id<MTLCommandBuffer> finished) {
            f->completed.store(finished.status == MTLCommandBufferStatusCompleted, std::memory_order_release);
            f->in_flight.store(false, std::memory_order_release);
        }];
        const auto dispatch = [](id<MTLComputeCommandEncoder> e, uint32_t n) {
            [e dispatchThreadgroups:MTLSizeMake(ceil_div(n, 256), 1, 1) threadsPerThreadgroup:MTLSizeMake(256, 1, 1)];
            [e endEncoding];
        };
        const auto set_projected = [&](id<MTLComputeCommandEncoder> e) {
            [e setBuffer:count ? projected.buffer : scratch->counts offset:count ? projected.offset : 0 atIndex:0];
        };
        const auto encode_sort = [&](uint32_t passes, uint32_t first_shift, bool source_keys = false) {
            for (uint32_t pass = 0; pass < passes; ++pass) {
                const SortParameters sort{f->sort_blocks, first_shift + pass * 8};
                const uint32_t src = pass % 2, dst = 1 - src;
                // Each live block writes all 256 digits into a compact histogram.
                // GPU-sized scans never read inactive reserved entries, including
                // when a large frame is followed by a small or fully culled one.
                auto e = impl_->begin(command, source_keys ? "source_histogram" : "tile_histogram", profile, GpuStage::Sort);
                [e setBuffer:scratch->keys[src] offset:0 atIndex:0];
                [e setBuffer:scratch->histogram offset:0 atIndex:1];
                [e setBuffer:f->status offset:0 atIndex:2];
                [e setBytes:&sort length:sizeof(sort) atIndex:3];
                [e dispatchThreadgroupsWithIndirectBuffer:scratch->dispatch_args indirectBufferOffset:0 threadsPerThreadgroup:MTLSizeMake(256, 1, 1)];
                [e endEncoding];
                e = impl_->begin(command, "digit_scan", profile, GpuStage::Sort);
                [e setBuffer:scratch->histogram offset:0 atIndex:0];
                [e setBuffer:scratch->histogram_offsets offset:0 atIndex:1];
                [e setBuffer:scratch->digit_offsets offset:0 atIndex:2];
                [e setBuffer:f->status offset:0 atIndex:3];
                [e dispatchThreadgroups:MTLSizeMake(256, 1, 1) threadsPerThreadgroup:MTLSizeMake(256, 1, 1)];
                [e endEncoding];
                e = impl_->begin(command, "digit_offsets", profile, GpuStage::Sort);
                [e setBuffer:scratch->digit_offsets offset:0 atIndex:0];
                [e dispatchThreadgroups:MTLSizeMake(1, 1, 1) threadsPerThreadgroup:MTLSizeMake(256, 1, 1)];
                [e endEncoding];
                e = impl_->begin(command, source_keys ? "source_scatter" : "tile_scatter", profile, GpuStage::Sort);
                [e setBuffer:scratch->keys[src] offset:0 atIndex:0];
                [e setBuffer:scratch->indices[src] offset:0 atIndex:1];
                [e setBuffer:scratch->keys[dst] offset:0 atIndex:2];
                [e setBuffer:scratch->indices[dst] offset:0 atIndex:3];
                [e setBuffer:scratch->histogram_offsets offset:0 atIndex:4];
                [e setBuffer:scratch->digit_offsets offset:0 atIndex:7];
                [e setBuffer:f->status offset:0 atIndex:5];
                [e setBytes:&sort length:sizeof(sort) atIndex:6];
                [e dispatchThreadgroupsWithIndirectBuffer:scratch->dispatch_args indirectBufferOffset:0 threadsPerThreadgroup:MTLSizeMake(256, 1, 1)];
                [e endEncoding];
            }
        };
        if (source_sorted) {
            auto e = impl_->begin(command, "source_keys", profile, GpuStage::Sort);
            set_projected(e);
            [e setBuffer:scratch->keys[1] offset:0 atIndex:1];
            [e setBuffer:scratch->indices[1] offset:0 atIndex:2];
            [e setBuffer:scratch->offsets offset:0 atIndex:3];
            [e setBytes:&p length:sizeof(p) atIndex:4];
            [e setBuffer:scratch->counts offset:0 atIndex:6];
            dispatch(e, count);
            const uint32_t groups = ceil_div(count, 256);
            impl_->scan(command, scratch->offsets, scratch->histogram, groups, scratch->count_scan, 0, profile);
            e = impl_->begin(command, "source_compact", profile, GpuStage::Sort);
            [e setBuffer:scratch->keys[1] offset:0 atIndex:0];
            [e setBuffer:scratch->indices[1] offset:0 atIndex:1];
            [e setBuffer:scratch->offsets offset:0 atIndex:2];
            [e setBuffer:scratch->histogram offset:0 atIndex:3];
            [e setBuffer:scratch->keys[0] offset:0 atIndex:4];
            [e setBuffer:scratch->indices[0] offset:0 atIndex:5];
            [e setBuffer:f->status offset:0 atIndex:6];
            [e setBuffer:scratch->dispatch_args offset:0 atIndex:7];
            [e setBytes:&p length:sizeof(p) atIndex:8];
            dispatch(e, count);
            encode_sort(4, 0, true);
            e = impl_->begin(command, "source_permutation", profile, GpuStage::Sort);
            [e setBuffer:scratch->indices[0] offset:0 atIndex:0];
            [e setBuffer:scratch->keys[1] offset:0 atIndex:1];
            [e setBuffer:f->status offset:0 atIndex:2];
            [e setBytes:&p length:sizeof(p) atIndex:3];
            dispatch(e, count);
        }
        // Source key generation also computes original tile counts while its
        // projected input is read sequentially. Gather only those 8-byte counts;
        // source-sorted counts and their scan reuse the existing two buffers.
        const auto counts = source_sorted ? scratch->offsets : scratch->counts;
        const auto offsets = source_sorted ? scratch->counts : scratch->offsets;
        if (count) {
            auto e = impl_->begin(command, "tile_counts", profile, GpuStage::Instances);
            set_projected(e);
            [e setBuffer:counts offset:0 atIndex:1];
            [e setBytes:&p length:sizeof(p) atIndex:2];
            [e setBuffer:scratch->keys[1] offset:0 atIndex:3];
            [e setBuffer:scratch->counts offset:0 atIndex:4];
            dispatch(e, count);
            impl_->scan(command, counts, offsets, count, scratch->count_scan, 0, profile);
        }
        auto e = impl_->begin(command, "tile_status", profile, GpuStage::Instances);
        [e setBuffer:counts offset:0 atIndex:0];
        [e setBuffer:offsets offset:0 atIndex:1];
        [e setBuffer:f->status offset:0 atIndex:2];
        [e setBytes:&p length:sizeof(p) atIndex:3];
        [e setBuffer:scratch->dispatch_args offset:0 atIndex:4];
        [e dispatchThreadgroups:MTLSizeMake(1, 1, 1) threadsPerThreadgroup:MTLSizeMake(1, 1, 1)];
        [e endEncoding];
        if (count) {
            e = impl_->begin(command, "tile_instances", profile, GpuStage::Instances);
            set_projected(e);
            [e setBuffer:offsets offset:0 atIndex:1];
            [e setBuffer:f->status offset:0 atIndex:2];
            [e setBuffer:scratch->keys[0] offset:0 atIndex:3];
            [e setBuffer:scratch->indices[0] offset:0 atIndex:4];
            [e setBytes:&p length:sizeof(p) atIndex:5];
            [e setBuffer:scratch->keys[1] offset:0 atIndex:6];
            dispatch(e, count);
            encode_sort((source_sorted ? 0u : 4u) + (std::bit_width(f->tiles - 1) + 7) / 8,
                        0u, source_sorted);
        }
        auto clear = [command blitCommandEncoder];
        [clear fillBuffer:scratch->ranges range:NSMakeRange(0, scratch->ranges.length) value:0];
        [clear endEncoding];
        if (count) {
            e = impl_->begin(command, source_sorted ? "source_ranges" : "tile_ranges", profile, GpuStage::Sort);
            const uint32_t sorted = (4 + (std::bit_width(f->tiles - 1) + 7) / 8) % 2;
            [e setBuffer:scratch->keys[sorted] offset:0 atIndex:0];
            [e setBuffer:scratch->ranges offset:0 atIndex:1];
            [e setBuffer:f->status offset:0 atIndex:2];
            [e dispatchThreadgroupsWithIndirectBuffer:scratch->dispatch_args indirectBufferOffset:3 * sizeof(uint32_t) threadsPerThreadgroup:MTLSizeMake(256, 1, 1)];
            [e endEncoding];
        }

        if (depth_batches) {
            auto clear_jobs = [command blitCommandEncoder];
            [clear_jobs fillBuffer:scratch->depth_jobs range:NSMakeRange(0, scratch->depth_jobs.length) value:255];
            [clear_jobs endEncoding];
            e = impl_->begin(command, "tile_depth_batches", profile, GpuStage::Blend);
            [e setBuffer:scratch->ranges offset:0 atIndex:0];
            [e setBuffer:scratch->depth_jobs offset:0 atIndex:1];
            [e setBytes:&p length:sizeof(p) atIndex:2];
            [e setBuffer:f->status offset:0 atIndex:3];
            dispatch(e, f->tiles);
        }
        e = profiledCompute(command, profile, GpuStage::Blend);
        if (!e)
            throw std::runtime_error(std::format("Could not encode Metal viewer blend pass (command_status={}, count={}, extent={}x{})", long(command.status), count, f->width, f->height));
        e.label = @"tile_blend";
        [e setComputePipelineState:blend_pipeline];
        set_projected(e);
        const uint32_t sorted = (4 + (std::bit_width(f->tiles - 1) + 7) / 8) % 2;
        [e setBuffer:scratch->depth_jobs ?: scratch->counts offset:0 atIndex:13];
        [e setBuffer:scratch->partial_color ?: scratch->counts offset:0 atIndex:14];
        [e setBuffer:scratch->partial_depth ?: scratch->counts offset:0 atIndex:15];
        [e setBuffer:scratch->partial_pick ?: scratch->counts offset:0 atIndex:16];
        [e setBuffer:gut.buffer ?: scratch->counts offset:gut.buffer ? gut.offset : 0 atIndex:10];
        [e setBuffer:scratch->indices[sorted] offset:0 atIndex:1];
        [e setBuffer:scratch->ranges offset:0 atIndex:2];
        [e setBuffer:f->status offset:0 atIndex:3];
        [e setBytes:&p length:sizeof(p) atIndex:4];
        const std::array<BufferSlice, 5> overlays = {overlay.parameters, overlay.flags, overlay.selection, overlay.preview, overlay.colors};
        for (NSUInteger j = 0; j < overlays.size(); ++j)
            [e setBuffer:overlays[j].buffer ?: scratch->counts offset:overlays[j].buffer ? overlays[j].offset : 0 atIndex:5 + j];
        [e setBuffer:lod.enabled && logical.buffer ? logical.buffer : scratch->counts offset:lod.enabled && logical.buffer ? logical.offset : 0 atIndex:11];
        [e setBytes:&logical_count length:sizeof(logical_count) atIndex:12];
        [e setTexture:f->color atIndex:0];
        [e setTexture:f->depth atIndex:1];
        [e setTexture:f->pick atIndex:2];

        if (depth_batches) {
            [e setComputePipelineState:prefix_pipeline];
            [e dispatchThreadgroups:MTLSizeMake(size_t(f->tiles) * 8, 1, 1) threadsPerThreadgroup:MTLSizeMake(32, 1, 1)];
            [e memoryBarrierWithScope:MTLBarrierScopeBuffers];
            [e setComputePipelineState:blend_pipeline];
            [e dispatchThreadgroupsWithIndirectBuffer:scratch->dispatch_args indirectBufferOffset:18 * sizeof(uint32_t) threadsPerThreadgroup:MTLSizeMake(32, 1, 1)];
        } else
            [e dispatchThreadgroups:MTLSizeMake(size_t(f->tiles) * (single_simd ? 8 : 4), 1, 1) threadsPerThreadgroup:MTLSizeMake(single_simd ? 32 : 64, 1, 1)];
        [e endEncoding];
        if (depth_batches) {
            e = impl_->begin(command, "tile_depth_compose", profile, GpuStage::Blend);
            set_projected(e);
            [e setBuffer:scratch->indices[sorted] offset:0 atIndex:1];
            [e setBuffer:scratch->ranges offset:0 atIndex:2];
            [e setBuffer:f->status offset:0 atIndex:3];
            [e setBytes:&p length:sizeof(p) atIndex:4];
            [e setBuffer:scratch->partial_color offset:0 atIndex:14];
            [e setBuffer:scratch->partial_depth offset:0 atIndex:15];
            [e setBuffer:scratch->partial_pick offset:0 atIndex:16];
            [e setTexture:f->color atIndex:0];
            [e setTexture:f->depth atIndex:1];
            [e setTexture:f->pick atIndex:2];
            [e dispatchThreadgroups:MTLSizeMake(size_t(f->tiles) * 8, 1, 1) threadsPerThreadgroup:MTLSizeMake(32, 1, 1)];
            [e endEncoding];
        }
    }
} // namespace lfs::rendering::metal
